= SRFI nnn
:toc: macro
:toc-title:

== Title

*ASCII character library*

== Author

Lassi Kortela

== Status

Draft

== Abstract

This SRFI defines ASCII-only equivalents to many of the character
procedures in standard Scheme plus a few extra ones. Recent Scheme
standards are based around Unicode but the significant syntactic
elements in many file formats and network protocols are all ASCII.
Such low-level code can be faster and easier to understand with ASCII
primitives.

== Rationale

Procedures dealing with character objects have been included in
standard Scheme since R^2^RS (1985) with identical arguments and
return values. The early Scheme reports did not mandate any particular
character set, though in practice most (perhaps all) implementations
used extended ASCII. R^6^RS (2007) was the first standard to strongly
favor Unicode.

Unicode is a fine choice for high-level work, but is overkill for most
low-level work dealing with file formats and network protocols.
ASCII-only procedures are much simpler to implement and their behavior
is much easier to understand than their Unicode equivalents. They have
shorter code paths with fewer and simpler failure modes, and need no
lookup tables.

=== Characters as fixnums

Scheme has a standard character data type which is very useful for
disambiguating between characters and integers. However, code dealing
with low-level binary formats typically uses byte ports and
bytevectors whose elements are small integers (fixnums). It is
convenient to treat those fixnums as if they were characters (which
they often represent, as most binary formats also contain strings of
text). For that reason, most of the procedures in this SRFI taking
character objects also accept fixnums in their place.

=== Procedure equivalence

The following table lists all procedures defined in this SRFI that
have direct equivalents in the Scheme R^n^RS standards.

[options="header"]
|=======
|This SRFI|R^n^RS|Since
|*ascii-alphabetic?*|*char-alphabetic?*|R^2^RS
|*ascii-numeric?*|*char-numeric?*|R^2^RS
|*ascii-whitespace?*|*char-whitespace?*|R^2^RS
|*ascii-upper-case?*|*char-upper-case?*|R^2^RS
|*ascii-lower-case?*|*char-lower-case?*|R^2^RS
|*ascii-upcase*|*char-upcase*|R^2^RS
|*ascii-downcase*|*char-downcase*|R^2^RS
|*ascii-digit-value*|*digit-value*|R^7^RS*
|=======

Note that the *ascii-digit-value* procedure takes a _limit_ argument
that the standard *digit-value* procedure does not take.

The standard Scheme procedures listed above require their arguments to
be character objects. The equivalents in this SRFI accept fixnums in
addition to character objects.

=== Capsule history of ASCII

The ASCII (American Standard Code for Information Interchange)
character set is standardized by ANSI (American National Standards
Institute). The first ASCII standard was published in 1963. The
organization was not yet called ANSI back then; its name was ASA
(American Standards Association).

Most computers now deal with 8-bit bytes, and ASCII is often thought
of as an 8-bit character set. However, it is actually only 7-bit. The
8th bit was left unused because 8-bit hardware was not yet ubiquitous
in the sixties. Through the decades many applications have used the
8th bit as a parity or flag bit.

Once international character sets were created, most of them took the
7-bit ASCII code as a basis. 8-bit character sets for alphabets
generally took ASCII as the first half, using the other half for
national letters as well as typographic elements and more control
characters. Multi-byte character sets for complex writing systems are
also generally based on ASCII but encoding them into 8-bit bytes is
more complex. UTF-8, the dominant encoding of Unicode, is a multi-byte
character encoding where 8-bit bytes using only the low 7 bits
represent ASCII characters.

More complete histories of ASCII are available on Wikipedia and
numerous other places. Of particular interest is that these histories
explain why the allocation of character codes is almost perfectly
logical but not quite.

=== ASCII character table

    #x00 NUL  #x10 DLE  #x20    #x30 0  #x40 @  #x50 P  #x60 `  #x70 p
    #x01 SOH  #x11 DC1  #x21 !  #x31 1  #x41 A  #x51 Q  #x61 a  #x71 q
    #x02 STX  #x12 DC2  #x22 "  #x32 2  #x42 B  #x52 R  #x62 b  #x72 r
    #x03 ETX  #x13 DC3  #x23 #  #x33 3  #x43 C  #x53 S  #x63 c  #x73 s
    #x04 EOT  #x14 DC4  #x24 $  #x34 4  #x44 D  #x54 T  #x64 d  #x74 t
    #x05 ENQ  #x15 NAK  #x25 %  #x35 5  #x45 E  #x55 U  #x65 e  #x75 u
    #x06 ACK  #x16 SYN  #x26 &  #x36 6  #x46 F  #x56 V  #x66 f  #x76 v
    #x07 BEL  #x17 ETB  #x27 '  #x37 7  #x47 G  #x57 W  #x67 g  #x77 w
    #x08 BS   #x18 CAN  #x28 (  #x38 8  #x48 H  #x58 X  #x68 h  #x78 x
    #x09 HT   #x19 EM   #x29 )  #x39 9  #x49 I  #x59 Y  #x69 i  #x79 y
    #x0a LF   #x1a SUB  #x2a *  #x3a :  #x4a J  #x5a Z  #x6a j  #x7a z
    #x0b VT   #x1b ESC  #x2b +  #x3b ;  #x4b K  #x5b [  #x6b k  #x7b {
    #x0c FF   #x1c FS   #x2c ,  #x3c <  #x4c L  #x5c \  #x6c l  #x7c |
    #x0d CR   #x1d GS   #x2d -  #x3d =  #x4d M  #x5d ]  #x6d m  #x7d }
    #x0e SO   #x1e RS   #x2e .  #x3e >  #x4e N  #x5e ^  #x6e n  #x7e ~
    #x0f SI   #x1f US   #x2f /  #x3f ?  #x4f O  #x5f _  #x6f o  #x7f DEL

=== ASCII character classes

    #x00..#x1f  control         #x20        space
    #x21..#x2f  punctuation     #x30..#x39  digit
    #x3a..#x40  punctuation     #x41..#x5a  upper-case
    #x5b..#x60  punctuation     #x61..#x7a  lower-case
    #x7b..#x7e  punctuation     #x7f        control

== Specification

=== Character class constants

*ascii-digits*

A string constant consisting of all ASCII characters in the _digit_
class, in increasing order by ASCII value.

*ascii-lower-case*

A string constant consisting of all ASCII characters in the
_lower-case_ class, in increasing order by ASCII value.

*ascii-upper-case*

A string constant consisting of all ASCII characters in the
_upper-case_ class, in increasing order by ASCII value.

*ascii-punctuation*

A string constant consisting of all ASCII characters in the
_punctuation_ class, in increasing order by ASCII value.

=== Predicates specific to ASCII

(*ascii-char?* _obj_)

Returns `#t` if _obj_ is a character object representing an ASCII
character. Else returns `#f`.

In a Scheme implementation where character objects are Unicode,
character values less than `#x80` represent ASCII.

This is the only predicate in this SRFI that recognizes only character
objects, not equivalent fixnums.

(*ascii-control?* _char_)

Returns `#t` if _char_ represents an ASCII character in the _control_
class. Else returns `#f`.

Note that carriage return, line feed and tab are control characters
but space is not.

_char_ can be a fixnum or character object.

(*ascii-display?* _char_)

Returns `#t` if _char_ represents an ASCII character that is *not* in
the _control_ class. Else returns `#f`.

The point is that display characters are safe to write to a device
that may not be able to sensibly interpret control characters or
non-ASCII characters.

Note that we consider space to be a display character but not tab,
carriage return or line feed. This convention is popular but not
universal.

_char_ can be a fixnum or character object.

(*ascii-space-or-tab?* _char_)

Returns `#t` if _char_ represents an ASCII character with the integer
value `#x09` (tab) or `#x20` (space). Else returns `#f`.

The point is that space and tab are very often useful to distinguish
from other whitespace characters, notably newlines.

_char_ can be a fixnum or character object.

(*ascii-punctuation?* _char_)

Returns `#t` if _char_ represents an ASCII character in the
_punctuation_ class. Else returns `#f`.

_char_ can be a fixnum or character object.

(*ascii-alphanumeric?* _char_)

Returns `#t` if _char_ represents an ASCII character in the
_upper-case_ or _lower-case_ or _digit_ class. Else returns `#f`.

_char_ can be a fixnum or character object.

=== Predicates with standard Scheme equivalents

(*ascii-alphabetic?* _char_)

Returns `#t` if _char_ represents an ASCII character in the
_upper-case_ or _lower-case_ class. Else returns `#f`.

_char_ can be a fixnum or character object.

(*ascii-numeric?* _char_)

Returns `#t` if _char_ represents an ASCII character in the _digit_
class. Else returns `#f`.

_char_ can be a fixnum or character object.

(*ascii-whitespace?* _char_)

Returns `#t` if _char_ represents an ASCII character with the integer
value `#x09` (tab) or `#x0a` (line feed) or `#x0b` (vertical tab) or
`#x0c` (form feed) or `#x0d` (carriage return) or `#x20` (space). Else
returns `#f`.

Notice how the other whitespace characters form a contiguous range of
control characters, but space stands alone as a separate non-control
character.

_char_ can be a fixnum or character object.

(*ascii-upper-case?* _char_)

Returns `#t` if _char_ represents an ASCII character in the
_upper-case_ class. Else returns `#f`.

_char_ can be a fixnum or character object.

(*ascii-lower-case?* _char_)

Returns `#t` if _char_ represents an ASCII character in the
_lower-case_ class. Else returns `#f`.

_char_ can be a fixnum or character object.

=== Case conversion procedures

(*ascii-upcase* _char_)

If _char_ represents an ASCII character in the _lower-case_ class,
returns the same letter from the _upper-case_ class. Else returns
_char_ unchanged.

_char_ can be a fixnum or a character object; the same type of object
is returned.

(*ascii-downcase* _char_)

If _char_ represents an ASCII character in the _upper-case_ class,
returns the same letter from the _lower-case_ class. Else returns
_char_ unchanged.

_char_ can be a fixnum or a character object; the same type of object
is returned.

=== Transformation procedures

These procedures serve as versatile building blocks for various letter
and number transformations.

(*ascii-nth-digit* _n_)

Returns a character object representing the _n_'th decimal digit in
ASCII. _n_ counts from zero so that 0 returns `0` and 9 returns `9`.

_n_ is a fixnum. It is taken modulo 10 so values less than 0 or
greater than 9 are permitted. Use R^5^RS modulo (not remainder) when
implementing the procedures in this SRFI.

(*ascii-nth-upper-case* _n_)

Returns a character object representing the _n_'th letter in the
upper-case Latin alphabet in ASCII. _n_ counts from zero so that 0
returns `A` and 25 returns `Z`.

_n_ is a fixnum. It is taken modulo 26 so values less than 0 or
greater than 25 are permitted. Use R^5^RS modulo (not remainder) when
implementing the procedures in this SRFI.

(*ascii-nth-lower-case* _n_)

Returns a character object representing the _n_'th letter in the
lower-case Latin alphabet in ASCII. _n_ counts from zero so that 0
returns `a` and 25 returns `z`.

_n_ is a fixnum. It is taken modulo 26 so values less than 0 or
greater than 25 are permitted. Use R^5^RS modulo (not remainder) when
implementing the procedures in this SRFI.

(*ascii-digit-value* _char_ _limit_)

If _char_ represents an ASCII decimal digit, returns the numeric value
0..9 of that digit. Only digit values less than _limit_ are accepted:
for example, a _limit_ of 8 accepts only octal digits. If _char_ does
not represent an acceptable digit, `#f` is returned.

To accept the entire range, pass a _limit_ of 10. It is not an error
to pass a limit less than 1 or greater than 10. _limit_ must be a
fixnum, and can be negative. _char_ can be a fixnum or character
object.

(*ascii-upper-case-value* _char_ _offset_ _limit_)

If _char_ represents an ASCII upper-case letter, its distance from `A`
is taken as an integer 0..25. Only distances less than _limit_ are
accepted: for example, a _limit_ of 6 accepts only the letters
`ABCDEF`. An acceptable distance is returned with _offset_ added to
it; give an _offset_ of 0 to add nothing. If _char_ does not represent
an acceptable letter, `#f` is returned.

To accept the entire range, pass a _limit_ of 26. It is not an error
to pass a limit less than 1 or greater than 26. _offset_ and _limit_
must be fixnums, and can be negative. _char_ can be a fixnum or
character object.

(*ascii-lower-case-value* _char_ _offset_ _limit_)

If _char_ represents an ASCII lower-case letter, its distance from `a`
is taken as an integer 0..25. Only distances less than _limit_ are
accepted: for example, a _limit_ of 6 accepts only the letters
`abcdef`. An acceptable distance is returned with _offset_ added to
it; give an _offset_ of 0 to add nothing. If _char_ does not represent
an acceptable letter, `#f` is returned.

To accept the entire range, pass a _limit_ of 26. It is not an error
to pass a limit less than 1 or greater than 26. _offset_ and _limit_
must be fixnums, and can be negative. _char_ can be a fixnum or
character object.

== Examples

=== Case conversion

The case conversion procedures in this SRFI can be implemented in
terms of the rotation procedures.

[source,scheme]
----
(define (my-upcase char)   (or (ascii-lower-case-value char #x41 26) char))
(define (my-downcase char) (or (ascii-upper-case-value char #x61 26) char))
----

=== Number parsing

Since there are lots of slightly different number syntaxes, this SRFI
does not provide procedures to convert between numbers and strings.
Instead, the rotation procedures let you easily roll your own. Here is
one way to do it:

[source,scheme]
----
(define (parse-binary-digit  char) (ascii-digit-value char 2))
(define (parse-octal-digit   char) (ascii-digit-value char 8))
(define (parse-decimal-digit char) (ascii-digit-value char 10))

(define (parse-hex-digit char)
  (or (ascii-digit-value char 10)
      (ascii-lower-case-value char 10 6)
      (ascii-upper-case-value char 10 6)))

(define (quote-hex-digit n)
  (cond ((< n 10) (ascii-nth-digit n))
        ((< n 16) (ascii-nth-lower-case (- n 10)))))
----

=== Caesar cipher

The Caesar cipher is a naive encryption method used successfully in
ancient Rome. It involves rotating each letter by _rot_ alphabet
positions so that it becomes another letter. Letters rotated beyond
`Z` wrap around and resume counting from `A`; likewise, negative
rotations beyond `A` wrap around and resume from `Z`. ROT13 is a
Caesar variant that is its own inverse: a positive rotation by 13 is
identical to a negative rotation by -13. Non-alphabetic characters are
left intact.

[source,scheme]
----
(define (caesar-char rot char)
  (or (let ((n (ascii-lower-case-value char rot 26)))
        (and n (ascii-nth-lower-case n)))
      (let ((n (ascii-upper-case-value char rot 26)))
        (and n (ascii-nth-upper-case n)))
      char))
----

=== Strings utility

The Unix `strings` utility reads a binary file, looking for contiguous
sequences of ASCII bytes and showing each sequence as it is found. The
idea is to find human-readable text in the file. The following is the
main loop of `strings`. It relies on a `show` helper procedure that
displays `(list->string (map integer->char (reverse stride)))` if
`stride` is at least 4 bytes long.

[source,scheme]
----
(let loop ((stride '()))
  (let ((byte (read-u8 port)))
    (cond ((eof-object? byte)
           (show stride))
          ((not (ascii-display? byte))
           (show stride)
           (loop '()))
          (else
           (loop (cons byte stride))))))
----

== Implementation

A sample implementation is available at
github.com/scheme-requests-for-implementation/srfi-nnn. It provides
two equivalent libraries: one for R^6^RS and one for R^7^RS. Each
library depends only on standard language features. The R^6^RS library
uses number procedures specialized for fixnums. A test suite as well
as ready-to-run examples are included.

The R^6^RS code is a fully automatic conversion of the R^7^RS code.
The R^7^RS program doing the conversion is included.

The sample implementation has passed all its tests and successfully
run all the examples in at least the following Scheme implementations:

* Chez Scheme (R^6^RS)
* Chibi-Scheme (R^7^RS)
* Chicken (R^7^RS)
* Gauche (R^7^RS)
* Guile (R^6^RS)
* Kawa (R^7^RS)
* Larceny (R^6^RS and R^7^RS)
* Mosh (R^6^RS)
* Sagittarius (R^6^RS and R^7^RS)

== Copyright

Copyright © Lassi Kortela (2019).

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and associated documentation files (the
“Software”), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice (including the
next paragraph) shall be included in all copies or substantial
portions of the Software.

THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
